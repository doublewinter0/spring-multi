buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE'
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    version = '1.0'

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web', {
            exclude module: 'spring-boot-starter-tomcat'
        }
        compile 'org.springframework.boot:spring-boot-starter-jetty'
        compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
        testCompile 'junit:junit'
        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }
}

configure(subprojects.findAll {
    it.name == 'spring-restful' || it.name == 'spring-mybatis-xml' || it.name == 'spring-mybatis-multisrc' ||
            it.name == 'spring-mybatis-anno' || it.name == 'spring-mybatis-redis'
}) {
    dependencies {
        compile 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2'
        compile 'mysql:mysql-connector-java:8.0.11'
    }
}

configure(subprojects.findAll {
    it.name == 'spring-dubbo-server' || it.name == 'spring-dubbo-client'
}) {
    dependencies {
        // 这个依赖里的 javassist 包版本太低，与 Java8 及以上不兼容
        compile 'io.dubbo.springboot:spring-boot-starter-dubbo:1.0.0', {
            // exclude module: 'dubbo'
            // exclude module: 'zkclient'
        }
        // 改用如下的依赖
        compile 'com.alibaba:dubbo:2.6.2'
        compile 'com.101tec:zkclient:0.10'
        compile 'org.apache.zookeeper:zookeeper:3.4.12'
        compile 'org.apache.curator:curator-framework:4.0.1'
    }
}
