buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE'
    }
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    version = '1.0'

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-web', {
            exclude module: 'spring-boot-starter-tomcat'
        }
        compile 'org.springframework.boot:spring-boot-starter-jetty'
        compile 'org.springframework.boot:spring-boot-starter-thymeleaf'
        // JDK9+ 环境使用 SpringBoot 需添加的依赖
        // compile  'javax.xml.bind:jaxb-api:2.3.0'
        testCompile 'junit:junit'
        testCompile 'org.springframework.boot:spring-boot-starter-test'
    }
}

configure(subprojects.findAll {
    it.name == 'spring-restful' || it.name == 'spring-mybatis-xml' || it.name == 'spring-mybatis-multisrc' ||
            it.name == 'spring-mybatis-anno' || it.name == 'spring-mybatis-redis' || it.name == 'spring-solr'
}) {
    dependencies {
        compile 'org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.2'
        compile 'mysql:mysql-connector-java:8.0.11'
    }
}

configure(subprojects.findAll {
    it.name == 'spring-dubbo-server' || it.name == 'spring-dubbo-client'
}) {
    dependencies {
        compile 'ren.erdong:spring-boot-dubbo:2.0.0.RELEASE'
        compile 'org.apache.curator:curator-framework:4.0.1', {
            // exclude module: 'log4j'
        }
        compile 'org.apache.zookeeper:zookeeper:3.4.12', {
            // exclude module: 'slf4j-log4j12'
            // exclude module: 'log4j'
        }
    }
}

configurations {
    all*.exclude module: 'log4j'
    all*.exclude module: 'slf4j-log4j12'
}
